export ZSH=~/.files
export PROJECTS=~/Repositories

# zsh configuration files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Stash environment variables in ~/.localrc. This means they'll stay out
# of the main dotfiles repo (which may be public)
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

export NVM_DIR="/Users/jdelsman/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

[ -e /usr/local/etc/profile.d/z.sh ] && . /usr/local/etc/profile.d/z.sh
